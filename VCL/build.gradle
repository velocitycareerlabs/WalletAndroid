plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

import org.gradle.api.tasks.bundling.Jar

ext {
    publishCode = 163
    versionName = System.getenv("RELEASE_VERSION") ?: "2.7.3"
    artifactId = "vcl"
    groupId = "io.velocitycareerlabs"
}

group = groupId
version = versionName

android {
    namespace groupId
    compileSdk 35

    defaultConfig {
        minSdk 24
        targetSdk 35
        versionCode publishCode
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            minifyEnabled false
            shrinkResources false
            buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
            buildConfigField "int", "VERSION_CODE", "${defaultConfig.versionCode}"
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        rc {
            initWith debug
        }
        release {
            initWith debug
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    buildFeatures {
        buildConfig true
    }

    libraryVariants.configureEach { variant ->
        variant.outputs.all {
            outputFileName = "vcl-${project.version}.aar"
        }
    }
}

afterEvaluate {
    def sourcesJar = tasks.register("sourcesJar", Jar) {
        archiveClassifier.set("sources")
        from(android.sourceSets.main.java.srcDirs + android.sourceSets.main.kotlin.srcDirs)
        destinationDirectory = file("$buildDir/libs")
    }

    def javadocJar = tasks.register("javadocJar", Jar) {
        archiveClassifier.set("javadoc")
        def dummyDir = file("$buildDir/empty-javadoc")
        doFirst {
            dummyDir.mkdirs()
            new File(dummyDir, "placeholder.java").text = "/** Placeholder for javadoc */"
        }
        from(dummyDir)
        destinationDirectory = file("$buildDir/libs")
    }

    publishing {
        publications {
            create("library", MavenPublication) {
                groupId = project.group
                artifactId = artifactId
                version = project.version

                artifact(file("$buildDir/outputs/aar/vcl-${project.version}.aar"))
                artifact(sourcesJar.get())
                artifact(javadocJar.get())

                pom {
                    name.set("VCL")
                    description.set("Velocity Career Labs Android SDK")
                    url.set("https://github.com/velocitycareerlabs/WalletAndroid")
                    licenses {
                        license {
                            name.set("The Apache License, Version 2.0")
                            url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
                        }
                    }
                    developers {
                        developer {
                            id.set("michaelavoyan")
                            name.set("Michael Avoyan")
                            email.set("michael.avoyan@gmail.com")
                        }
                    }
                    scm {
                        connection.set("scm:git:https://github.com/velocitycareerlabs/WalletAndroid.git")
                        developerConnection.set("scm:git:ssh://git@github.com:velocitycareerlabs/WalletAndroid.git")
                        url.set("https://github.com/velocitycareerlabs/WalletAndroid")
                    }
                }
            }
        }

        repositories {
            maven {
                name = "localStagingForJReleaser"
                url = uri("$buildDir/staging-deploy")
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'

    implementation 'com.nimbusds:nimbus-jose-jwt:10.0.2'
    implementation "androidx.security:security-crypto:1.1.0-alpha07"

    // Reflection
//    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13.2'
    // Optional -- Robolectric environment
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'org.robolectric:robolectric:4.13'
    // Optional -- Mockito framework
    testImplementation "org.mockito:mockito-core:5.14.2"
    testImplementation "org.mockito:mockito-inline:5.2.0"
    // Required -- JSON framework
    testImplementation 'org.json:json:20240303'
    // json assert
    testImplementation 'org.skyscreamer:jsonassert:1.5.3'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
