plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
    id 'signing'
}

android {
    compileSdk 35
    namespace 'io.velocitycareerlabs'

    defaultConfig {
        minSdk 24
        targetSdk 35
        versionName "2.6.12"
        versionCode 156
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
//        def BOOLEAN = "boolean"
//        def TRUE = "true"
//        def FALSE = "false"
        debug {
            buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
            buildConfigField "int", "VERSION_CODE", "${defaultConfig.versionCode}"

//            useProguard false
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
            buildConfigField "int", "VERSION_CODE", "${defaultConfig.versionCode}"

//            useProguard true
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        rc {
            buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}-rc\""
            buildConfigField "int", "VERSION_CODE", "${defaultConfig.versionCode}"
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }
    buildFeatures {
        buildConfig true
    }
}

java {
    sourceCompatibility = JavaVersion.VERSION_1_8
    targetCompatibility = JavaVersion.VERSION_1_8
}

/**
 * Publish Android library ======================================================
*/
def getVersionName = { ->
    return android.defaultConfig.versionName
}
def getArtifactId = { ->
    return "vcl" // Must be lower case!
}
def getGroupId = { ->
    return 'io.velocitycareerlabs'
}

tasks.register('androidSourcesJar', Jar) {
    archiveClassifier.set('sources')
    from android.sourceSets.main.java.srcDirs
    from android.sourceSets.main.kotlin.srcDirs
}

artifacts {
    archives tasks.named('androidSourcesJar')
}

afterEvaluate {
    publishing {
        publications {
            create('rc', MavenPublication) {
                groupId = getGroupId()
                artifactId = getArtifactId()
                version = getVersionName()

                if (project.plugins.hasPlugin("com.android.library")) {
                    artifact("$buildDir/outputs/aar/${project.name}-release.aar") {
                        builtBy tasks.named('assembleRelease')
                    }
                } else {
                    artifact("$buildDir/libs/${project.name}.jar") {
                        builtBy tasks.named('jar')
                    }
                }

                artifact(tasks.named('androidSourcesJar'))

                pom {
                    name.set(getArtifactId())
                    description.set('Velocity Career Labs Android library')
                    url.set('https://github.com/velocitycareerlabs/WalletAndroid')
                    licenses {
                        license {
                            name.set('Apache License 2.0')
                            url.set('https://github.com/velocitycareerlabs/WalletAndroid/blob/dev/VCL/LICENSE')
                        }
                    }
                    developers {
                        developer {
                            id.set('velocitycareerlabs')
                            name.set('Michael Avoyan')
                            email.set('michael.avoyan@velocitycareerlabs.com')
                        }
                    }
                    scm {
                        connection.set('scm:github.com/velocitycareerlabs/WalletAndroid.git')
                        developerConnection.set('scm:git:ssh://github.com/velocitycareerlabs/WalletAndroid.git')
                        url.set('https://github.com/velocitycareerlabs/WalletAndroid')
                    }
                }
            }
            create('release', MavenPublication) {
                groupId = getGroupId()
                artifactId = getArtifactId()
                version = getVersionName()

                if (project.plugins.hasPlugin("com.android.library")) {
                    artifact("$buildDir/outputs/aar/${project.name}-release.aar") {
                        builtBy tasks.named('assembleRelease')
                    }
                } else {
                    artifact("$buildDir/libs/${project.name}.jar") {
                        builtBy tasks.named('jar')
                    }
                }

                artifact(tasks.named('androidSourcesJar'))

                pom {
                    name.set(getArtifactId())
                    description.set('Velocity Career Labs Android library')
                    url.set('https://github.com/velocitycareerlabs/WalletAndroid')
                    licenses {
                        license {
                            name.set('Apache License 2.0')
                            url.set('https://github.com/velocitycareerlabs/WalletAndroid/blob/dev/VCL/LICENSE')
                        }
                    }
                    developers {
                        developer {
                            id.set('velocitycareerlabs')
                            name.set('Michael Avoyan')
                            email.set('michael.avoyan@velocitycareerlabs.com')
                        }
                    }
                    scm {
                        connection.set('scm:github.com/velocitycareerlabs/WalletAndroid.git')
                        developerConnection.set('scm:git:ssh://github.com/velocitycareerlabs/WalletAndroid.git')
                        url.set('https://github.com/velocitycareerlabs/WalletAndroid')
                    }
                }
            }
        }
    }

    signing {
        useInMemoryPgpKeys(
                rootProject.ext.signingKeyId,
                rootProject.ext.signingPrivateKey,
                rootProject.ext.signingPassword
        )
        sign(publishing.publications)
//        sign(publishing.publications['rc'])
//        sign(publishing.publications['release'])
    }

    tasks.named('generateMetadataFileForRcPublication') {
        dependsOn(tasks.named('androidSourcesJar'))
    }

    tasks.named("publishRcPublicationToSonatypeRepository") {
        dependsOn(tasks.named("signRcPublication"))
        dependsOn(tasks.named("signReleasePublication"))
    }

    tasks.named("publishReleasePublicationToSonatypeRepository") {
        dependsOn(tasks.named("signReleasePublication"))
        dependsOn(tasks.named("signRcPublication"))
    }
}

repositories {
    mavenCentral()
}
/**
 * =================================================================================================
*/

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'

    implementation 'com.nimbusds:nimbus-jose-jwt:9.45'
    implementation "androidx.security:security-crypto:1.1.0-alpha06"

    // Reflection
//    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13.2'
    // Optional -- Robolectric environment
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'org.robolectric:robolectric:4.13'
    // Optional -- Mockito framework
    testImplementation "org.mockito:mockito-core:5.14.2"
    testImplementation "org.mockito:mockito-inline:5.2.0"
    // Required -- JSON framework
    testImplementation 'org.json:json:20240303'
    // json assert
    testImplementation 'org.skyscreamer:jsonassert:1.5.3'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}