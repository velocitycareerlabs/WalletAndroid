plugins {
    id 'com.android.library'
    id 'kotlin-android'
    id 'maven-publish'
}

import org.gradle.api.tasks.bundling.Jar

ext {
    publishCode = 163
    publishVersion = "2.7.3"
    artifactId = "vcl"
    publishGroup = "io.velocitycareerlabs"
    namespaceId = publishGroup
}

group = publishGroup
version = System.getenv("JRELEASER_PROJECT_VERSION") ?: publishVersion

android {
    compileSdk 35
    namespace namespaceId

    defaultConfig {
        minSdk 24
        targetSdk 35
        versionCode publishCode
        versionName version
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"
        consumerProguardFiles "consumer-rules.pro"
    }

    buildTypes {
        debug {
            buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
            buildConfigField "int", "VERSION_CODE", "${defaultConfig.versionCode}"
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        release {
            buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}\""
            buildConfigField "int", "VERSION_CODE", "${defaultConfig.versionCode}"
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        rc {
            buildConfigField "String", "VERSION_NAME", "\"${defaultConfig.versionName}-rc\""
            buildConfigField "int", "VERSION_CODE", "${defaultConfig.versionCode}"
            minifyEnabled false
            shrinkResources false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = '1.8'
    }

    testOptions {
        unitTests.includeAndroidResources = true
        unitTests.returnDefaultValues = true
    }

    buildFeatures {
        buildConfig true
    }

    libraryVariants.configureEach { variant ->
        variant.outputs.all {
            outputFileName = "vcl-${variant.name}.aar"
        }
    }
}

afterEvaluate {
    def sourcesJar = tasks.register("sourcesJar", Jar) {
        dependsOn 'assemble'
        archiveBaseName.set("vcl")
        archiveVersion.set("") // REMOVE version from filename
        archiveClassifier.set("sources")
        destinationDirectory = file("$buildDir/libs")

        doFirst {
            def mainSrcSet = android.sourceSets.findByName("main")
            if (mainSrcSet != null && mainSrcSet.java != null && mainSrcSet.java.srcDirs != null) {
                from(mainSrcSet.java.srcDirs)
            }
        }
    }

    def javadocJar = tasks.register("javadocJar", Jar) {
        dependsOn 'assemble'
        archiveBaseName.set("vcl")
        archiveVersion.set("")
        archiveClassifier.set("javadoc")
        doFirst {
            file("$buildDir/empty-javadoc").mkdirs()
            if (file("$buildDir/empty-javadoc").exists()) {
                from("$buildDir/empty-javadoc")
            }
        }
        destinationDirectory = file("$buildDir/libs")
    }

    publishing {
        publications {
            release(MavenPublication) {
                groupId = project.group
                artifactId = artifactId
                version = project.version

                artifact("$buildDir/outputs/aar/${artifactId}-${version}.aar")
                artifact(sourcesJar.get())
                artifact(javadocJar.get())

                pom {
                    name.set("VCL")
                    description.set("Velocity Career Labs Android SDK")
                    url.set("https://github.com/velocitycareerlabs/WalletAndroid")
                    licenses {
                        license {
                            name.set("The Apache License, Version 2.0")
                            url.set("https://www.apache.org/licenses/LICENSE-2.0.txt")
                        }
                    }
                    developers {
                        developer {
                            id.set("michaelavoyan")
                            name.set("Michael Avoyan")
                            email.set("michael.avoyan@gmail.com")
                        }
                    }
                    scm {
                        connection.set("scm:git:https://github.com/velocitycareerlabs/WalletAndroid.git")
                        developerConnection.set("scm:git:ssh://git@github.com:velocitycareerlabs/WalletAndroid.git")
                        url.set("https://github.com/velocitycareerlabs/WalletAndroid")
                    }
                }
            }
        }
    }
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'androidx.core:core-ktx:1.15.0'

    implementation 'com.nimbusds:nimbus-jose-jwt:10.0.2'
    implementation "androidx.security:security-crypto:1.1.0-alpha07"

    // Reflection
//    implementation "org.jetbrains.kotlin:kotlin-reflect:$kotlin_version"

    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlin_version"

    // Required -- JUnit 4 framework
    testImplementation 'junit:junit:4.13.2'
    // Optional -- Robolectric environment
    testImplementation 'androidx.test:core:1.6.1'
    testImplementation 'org.robolectric:robolectric:4.13'
    // Optional -- Mockito framework
    testImplementation "org.mockito:mockito-core:5.14.2"
    testImplementation "org.mockito:mockito-inline:5.2.0"
    // Required -- JSON framework
    testImplementation 'org.json:json:20240303'
    // json assert
    testImplementation 'org.skyscreamer:jsonassert:1.5.3'

    androidTestImplementation 'androidx.test.ext:junit:1.2.1'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.6.1'
}
